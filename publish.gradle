apply plugin: 'maven-publish'

// allow javadoc archive on Java 8 even if the javadoc isn't complete
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

// source jar
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
}

// javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

// publishing
publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'remapper'

            artifact jar
            artifact sourcesJar
            artifact javadocJar

            repositories {
                maven {
                    // credentials
                    credentials {
                        username System.getenv("sonatypeUsername")
                        password System.getenv("sonatypePassword")
                    }
                    // url depends on version
                    if (version.endsWith('-SNAPSHOT')) {
                        url = 'http://repo.glowstone.net/content/repositories/snapshots/'
                    } else {
                        url = 'http://repo.glowstone.net/content/repositories/releases/'
                    }
                }
            }
        }
    }
}
